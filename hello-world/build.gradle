apply plugin: 'spring-boot'
apply plugin: 'idea'
apply plugin: 'war'

apply from: 'https://raw.githubusercontent.com/abhijitsarkar/build-tools/master/gradle-common/build.gradle'

group = 'name.abhijitsarkar.javaee'
version = '0.0.1-SNAPSHOT'

// Rename the property used to choose property files
project.ext.propertiesPluginEnvironmentNameProperty = 'env'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath(
                ["org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE"],
                ["net.saliman:gradle-properties-plugin:1.4.4"]
        )
    }
}

apply plugin: 'net.saliman.properties'

repositories {
    mavenCentral()
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.projectlombok:lombok:1.16.6")

    runtime("mysql:mysql-connector-java")

    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
}

task herokuEnv(type: Copy) {
    from(projectDir) {
        include "gradle-${env}.properties"
        rename("gradle-${env}.properties", '.env')
    }
    into projectDir
}

task stage {
    dependsOn dependsOn: ['build', 'herokuEnv']
}

println "Env: ${env}, Properties: ${project.filterTokens}"

springBoot {
    mainClass = 'name.abhijitsarkar.javaee.hello.HelloWorldApp'
}

bootRun {
    systemProperties = [
            'JDBC_DATABASE_URL'           : project.filterTokens.JDBC_DATABASE_URL,
            'JDBC_DATABASE_USERNAME'      : project.filterTokens.JDBC_DATABASE_USERNAME,
            'JDBC_DATABASE_PASSWORD'      : project.filterTokens.JDBC_DATABASE_PASSWORD,
            'spring.datasource.initialize': project.filterTokens['spring.datasource.initialize'],
            'PORT'                        : project.filterTokens['PORT']
    ]
}

